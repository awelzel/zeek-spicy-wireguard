# This is pretty much a straightforward implementation following https://www.wireguard.com/protocol/

module zeek_spicy_wireguard;

import spicy;

%byte-order = spicy::ByteOrder::Little;

type MessageType = enum {
	handshake_initiation = 1,
	handshake_response = 2,
	packet_cookie_reply = 3,
	packet_data = 4
};

function AEAD_LEN(num: uint64) : uint64 {
	return num + 16;
}

public type WireGuardPacket = unit {
	message_type: uint8;
	reserved_zero: bytes &size=3 &requires=($$ == b"\x00\x00\x00");
	switch ( MessageType(self.message_type) ) {
		MessageType::handshake_initiation -> handshake_initiation: HandshakeInitiation;
		MessageType::handshake_response -> handshake_response: HandshakeResponse;
		MessageType::packet_cookie_reply -> packet_cookie_reply: PacketCookieReply;
		MessageType::packet_data -> packet_data: PacketData;
	};
};

type HandshakeInitiation = unit {
	sender_index: uint32;
	unencrypted_ephemeral: bytes &size=32;
	encrypted_static: bytes &size=AEAD_LEN(32);
	encrypted_timestamp: bytes &size=AEAD_LEN(12);
	mac1: bytes &size=16;
	mac2: bytes &size=16;
	nothing: bytes &eod &requires=(|$$| == 0);
};

type HandshakeResponse = unit {
	sender_index: uint32;
	receiver_index: uint32;
	unencrypted_ephemeral: bytes &size=32;
	encrypted_nothing: bytes &size=AEAD_LEN(0);
	mac1: bytes &size=16;
	mac2: bytes &size=16;
	nothing: bytes &eod &requires=(|$$| == 0);
};

type PacketCookieReply = unit {
	receiver_index: uint32;
	nonce: bytes &size=24;
	encrypted_cookie: bytes &size=AEAD_LEN(16);
	nothing: bytes &eod &requires=(|$$| == 0);
};

type PacketData = unit {
	receiver_index: uint32;
	counter: uint64;
	encrypted_encapsulated_packet: bytes &eod;
};

%byte-order = spicy::ByteOrder::Big;

public type TailscalePacket = unit {
	magic: bytes &size=1 {
		local _magic = $$.to_uint(spicy::ByteOrder::Big);
		self.has_discovery = _magic == 0x54;
		self.has_wireguard = _magic == 0x01 || _magic == 0x02 || _magic == 0x03 || _magic == 0x04;

		if (self.has_wireguard) {
			self.data.connect(new WireGuardPacket);
			self.data.write(self.magic);
		}
    }

	var has_wireguard: bool;
	var has_discovery: bool;

	disc: TSDiscoveryPacket if (self.has_discovery);

	: bytes &eod &chunked if (self.has_wireguard) {
		self.data.write($$);
	}

	sink data;
};

type TSDiscoveryPacket = unit {
	# We already ate a byte, so we only need the next 5 bytes here.
	magic: bytes &size=5 &requires=($$ == b"\x53\xf0\x9f\x92\xac");
	senderDiscoPub: bytes &size=32;
	nonce: bytes &size=24;
	payload: bytes &eod;
} &byte-order = spicy::ByteOrder::Big;
